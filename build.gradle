buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.6.RELEASE"
		classpath "io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE"
		classpath "se.transmode.gradle:gradle-docker:1.2"
	}
}

subprojects {
	apply plugin: "spring-boot"
	apply plugin: "application"
	apply plugin: "docker"
	apply plugin: "io.spring.dependency-management"

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	configurations {
		all*.exclude module: 'spring-boot-starter-tomcat'
	}

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom 'org.springframework.boot:spring-boot-dependencies:1.4.1.RELEASE'
			mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR2'
		}
	}

	docker {
		baseImage 'anapsix/alpine-java'
		maintainer 'Lieven Doclo'
	}
}

project(":zuul-proxy") {
	mainClassName = "be.insaneprogramming.devoxx.ZuulProxy"

	dependencies {
		compile 'org.springframework.cloud:spring-cloud-starter-config'
		compile 'org.springframework.cloud:spring-cloud-starter-zuul'
		compile 'org.springframework.cloud:spring-cloud-starter-eureka'
		compile 'org.springframework.boot:spring-boot-starter-undertow'
	}

	distDocker {
		applicationName = "be.insaneprogramming.devoxx-bof-zuul-proxy"
		exposePort 8080
	}
}

project(":eureka-server") {
	mainClassName = "be.insaneprogramming.devoxx.EurekaServer"

	dependencies {
		compile 'org.codehaus.groovy:groovy-all:2.1.3'
		compile 'org.springframework.cloud:spring-cloud-starter-eureka-server'
		compile('org.springframework.boot:spring-boot-starter-undertow')
		testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
	}

	distDocker {
		applicationName = "be.insaneprogramming.devoxx-bof-eureka-server"
		exposePort 8761
	}
}

project(":config-server") {
	mainClassName = "be.insaneprogramming.devoxx.ConfigServer"

	dependencies {
		compile 'org.springframework.cloud:spring-cloud-config-server'
		compile 'org.springframework.cloud:spring-cloud-starter-eureka'
		compile 'org.springframework.boot:spring-boot-starter-undertow'
	}

	distDocker {
		applicationName = "be.insaneprogramming.devoxx-bof-config-server"
		exposePort 8888
	}
}

project(":service-one") {
	mainClassName = "be.insaneprogramming.devoxx.ServiceOneEndpoint"

	dependencies {
		compile 'org.springframework.cloud:spring-cloud-starter-config'
		compile 'org.springframework.cloud:spring-cloud-starter-eureka'
		compile 'org.springframework.boot:spring-boot-starter-undertow'
		compile 'org.springframework.boot:spring-boot-starter-web'
	}

	distDocker {
		applicationName = "be.insaneprogramming.devoxx-bof-service-one"
		exposePort 8082
	}
}

project(":service-two") {
	mainClassName = "be.insaneprogramming.devoxx.ServiceTwoEndpoint"

	dependencies {
		compile 'org.springframework.cloud:spring-cloud-starter-config'
		compile 'org.springframework.cloud:spring-cloud-starter-eureka'
		compile 'org.springframework.cloud:spring-cloud-starter-feign'
		compile 'org.springframework.cloud:spring-cloud-starter-hystrix'
		compile 'org.springframework.boot:spring-boot-starter-undertow'
		compile 'org.springframework.boot:spring-boot-starter-web'
	}

	distDocker {
		applicationName = "be.insaneprogramming.devoxx-bof-service-two"
		exposePort 8083
	}
}